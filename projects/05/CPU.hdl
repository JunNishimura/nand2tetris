// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:

    // if instruction[15]==0, then use a-instruction
    // if instruction[15]==1, then use c-instruction
    Mux16(a=instruction, b=outALU, sel=instruction[15], out=o1); 
    
    // set ARegister
    Not (in=instruction[15], out=nIN15);
    Or (a=nIN15, b=instruction[5], out=loadAR);
    ARegister(in=o1, load=loadAR, out=ar, out[0..14]=addressM);

    // set Program Counter
    PC (in=ar, load=loadPC, inc=true, reset=reset, out[0..14]=pc);
    
    // select A or M register
    Mux16(a=ar, b=inM, sel=instruction[12], out=o2);

    // set DRegister
    And (a=instruction[15], b=instruction[4], out=loadDR);
    DRegister(in=outALU, load=loadDR, out=dr);

    ALU (
        x=dr, 
        y=o2, 
        zx=instruction[11], 
        nx=instruction[10], 
        zy=instruction[9], 
        ny=instruction[8], 
        f=instruction[7], 
        no=instruction[6], 
        out=outM, 
        out=outALU, 
        zr=zr, 
        ng=ng
    );

    And(a=instruction[15], b=instruction[3], out=writeM);

    Not (in=ng, out=pos); // positive
    Not (in=zr, out=nzr); // non zero
    And (a=instruction[0], b=pos, out=jgt); // JGT 
    And (a=instruction[1], b=zr,  out=jeq); // JEQ 
    And (a=instruction[2], b=ng,  out=jlt); // JLT 
    Or  (a=jgt, b=jlt, out=pc1);
    And (a=nzr, b=pc1, out=pc2);
    Or  (a=pc2, b=jeq, out=pc3);
    And (a=pc3, b=instruction[15], out=loadPC);
}